/* CUP file for generating a parser */
/* http://www2.cs.tum.edu/projects/cup/ */

package nz.ac.auckland.labtex.generated;
import java_cup.runtime.*;
class LabParser;
action code {:
    Integer sheetnumber = 0;
    Integer horizontalnumber = 0;
    Integer verticalnumber = 0;
    String element = null;
    String prev = null;
:}

/* terminals */
terminal         TITLE, BEGIN, END;
terminal         LCURLY, RCURLY;
terminal         LABLETTEXT, LABLETHEADER, LABLETCHECK, HORIZONTALTWO;
terminal String  STRING;

/* non-terminals */
non terminal    lab, sheets, elem, elems, named_elem, named_elems;
non terminal    begin, horizontal, vertical;

/* grammar */
lab ::=
            TITLE
            {: System.out.println("function Lablet.buildActivity(builder)"); :}
            sheets
            {: System.out.println("end"); :}
		    |
            {: System.out.println("function Lablet.buildActivity(builder)"); :}
            sheets
            {: System.out.println("end"); :}
            ;
sheets ::=
            sheets begin elems END
		    |
		    begin elems END ;
begin ::=
            BEGIN LCURLY STRING:s
            {:
                ++sheetnumber;
                System.out.println();
                System.out.println("\t-- SHEET "+sheetnumber);
                System.out.println("\tlocal sheet = builder:create(\"Sheet\")");
                System.out.println("\tbuilder:add(sheet)");
                System.out.println("\tsheet:setTitle(\""+s+"\")\n");
            :}
            RCURLY
            |
            BEGIN
            {:
                ++sheetnumber;
                System.out.println();
                System.out.println("\t-- SHEET "+sheetnumber);
                System.out.println("\tlocal sheet = builder:create(\"Sheet\")");
                System.out.println("\tbuilder:add(sheet)");
            :}
            ;
elems ::=
            elem elems
            | elem ;
elem ::=
            LABLETTEXT LCURLY STRING:s
            {:
                System.out.println("\tsheet:addText(\""+s+"\")");
            :}
            RCURLY
            |
            LABLETHEADER LCURLY STRING:s
            {:
                System.out.println("\tsheet:addHeader(\""+s+"\")");
            :}
            RCURLY
            |
            LABLETCHECK LCURLY STRING:s
            {:
                System.out.println("\tsheet:addCheckQuestion(\""+s+"\")");
            :}
            RCURLY
            |
            horizontal
            ;
named_elems ::=
            named_elem named_elems
            | named_elem ;
named_elem ::=
            LABLETTEXT LCURLY STRING:s
            {:
                System.out.println("\tsheet:addText(\""+s+"\", "+element+")");
            :}
            RCURLY
            |
            LABLETHEADER LCURLY STRING:s
            {:
                System.out.println("\tsheet:addHeader(\""+s+"\", "+element+")");
            :}
            RCURLY
            |
            LABLETCHECK LCURLY STRING:s
            {:
                System.out.println("\tsheet:addCheckQuestion(\""+s+"\", "+element+")");
            :}
            RCURLY
            |
            horizontal
            ;
horizontal ::=
            HORIZONTALTWO
            {:
                Integer h = ++horizontalnumber;
                Integer n = sheetnumber;
                element = "horizontalLayout";
                System.out.println();
                System.out.println("\tlocal "+element+" = sheet:addHorizontalGroupLayout()");
            :}
            LCURLY vertical RCURLY
            LCURLY vertical RCURLY
            ;
vertical ::=
            {:
                Integer v = ++verticalnumber;
                prev = element;
                element = "verticalLayout";
                System.out.println();
                System.out.println("\tlocal "+element+" = sheet:addVerticalGroupLayout("+prev+")");
            :}
            named_elems
            {: element = prev; :}
            ;
