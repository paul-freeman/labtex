/* CUP file for generating a parser */
/* http://www2.cs.tum.edu/projects/cup/ */

package nz.ac.auckland.labtex.generated;
import java_cup.runtime.*;
class LabParser;
action code {:
    Integer sheetnumber = 0;
    Integer horizontalnumber = 0;
    Integer verticalnumber = 0;
    Integer videonumber = 0;
    String element = null;
    String prev = null;
:}

/* keywords */
terminal    KW_TITLE,
            KW_BEGIN,
            KW_END,
            KW_LABLETSHEET,
            KW_LABLETVIDEOPAGE,
            KW_LABLETMOTIONPAGE,
            KW_LABLETCALCYSPEED,
            KW_LABLETTEXT,
            KW_LABLETHEADER,
            KW_LABLETCHECK,
            KW_LABLETVIDEO,
            KW_HORIZONTAL,
            KW_HORIZONTALTWO,
            KW_HORIZONTALTHREE;

/* symbold */
terminal    LCURLY,
            RCURLY,
            BACKSLASH;
terminal String  LATEXSTRING, SQUARESTRING;

/* non-terminals */
non terminal    lab, sheets, sheet, elem, elems, named_elem, named_elems;
non terminal    normal_sheet, video_sheet, motion_sheet, calc_y_sheet;
non terminal    horizontal, vertical;
non terminal    title;

/* grammar */

// a lab is a title with a number of sheets
lab ::= title sheets {: System.out.println("end"); :};

// titles are always in one format
title ::=   KW_TITLE LCURLY LATEXSTRING:ls
            {:  System.out.print("Lablet = {\n\tinterface = 1.0,\n\ttitle = ");
                System.out.print("\"(TeX) " + ls + "\"\n}\n\n\n");
                System.out.println("function Lablet.buildActivity(builder)"); :}
            RCURLY
            ;

// one or more sheets
sheets ::= sheet sheets | sheet ;

// three kinds of sheets
sheet ::= normal_sheet | video_sheet | motion_sheet | calc_y_sheet;

normal_sheet ::=    KW_BEGIN LCURLY KW_LABLETSHEET RCURLY LCURLY LATEXSTRING:ls RCURLY
                    {:  ++sheetnumber;
                        System.out.println();
                        System.out.println("\t-- SHEET "+sheetnumber);
                        System.out.println("\tlocal sheet = builder:create(\"Sheet\")");
                        System.out.println("\tbuilder:add(sheet)");
                        System.out.println("\tsheet:setTitle(\"" + ls + "\")"); :}
                    elems ;

video_sheet ::=     KW_BEGIN LCURLY KW_LABLETVIDEOPAGE RCURLY LCURLY LATEXSTRING:ls RCURLY
                    {:  ++sheetnumber;
                        System.out.println();
                        System.out.println("\t-- SHEET "+sheetnumber);
                        System.out.println("\tlocal sheet = builder:create(\"Sheet\")");
                        System.out.println("\tbuilder:add(sheet)");
                        System.out.println("\tsheet:setTitle(\"" + ls + "\")"); :}
                    elems
                    |
                    KW_BEGIN LCURLY KW_LABLETVIDEOPAGE RCURLY LCURLY LATEXSTRING:ls RCURLY KW_HORIZONTAL
                    {:  ++sheetnumber;
                        System.out.println();
                        System.out.println("\t-- SHEET "+sheetnumber);
                        System.out.println("\tlocal sheet = builder:create(\"Sheet\")");
                        System.out.println("\tbuilder:add(sheet)");
                        System.out.println("\tsheet:setMainLayoutOrientation(\"horizontal\")");
                        System.out.println("\tsheet:setTitle(\"" + ls + "\")"); :}
                    elems ;

motion_sheet ::=    KW_LABLETMOTIONPAGE LCURLY LATEXSTRING:l1 RCURLY
                                        LCURLY LATEXSTRING:l2 RCURLY
                                        LCURLY LATEXSTRING:l3 RCURLY
                    {:  ++sheetnumber;
                        System.out.println();
                        System.out.println("\t-- SHEET "+sheetnumber);
                        System.out.println("\tlocal sheet = builder:create(\"MotionAnalysis\")");
                        System.out.println("\tbuilder:add(sheet)");
                        System.out.println("\tsheet:setTitle(\"" + l1 + "\")");
                        System.out.println("\tsheet:setExperiment(" + l2 + ")");
                        System.out.println("\tsheet:setDescriptionText(\"" + l3 + "\")"); :}
                    ;

calc_y_sheet ::=    KW_LABLETCALCYSPEED LCURLY LATEXSTRING:l1 RCURLY
                                        LCURLY LATEXSTRING:l2 RCURLY
                                        LCURLY LATEXSTRING:l3 RCURLY
                    {:  ++sheetnumber;
                        System.out.println();
                        System.out.println("\t-- SHEET "+sheetnumber);
                        System.out.println("\tlocal sheet = builder:create(\"CalculateYSpeed\")");
                        System.out.println("\tbuilder:add(sheet)");
                        System.out.println("\tsheet:setExperiment(" + l2 + ")");
                        System.out.println("\tsheet:setTitle(\"" + l1 + "\")");
                        System.out.println("\tsheet:setHeader(\"" + l3 + "\")"); :}
                    ;

elems ::= elem elems | elem ;
elem ::=
            KW_LABLETTEXT LCURLY LATEXSTRING:s
            {:
                System.out.println("\tsheet:addText(\""+s+"\")");
            :}
            RCURLY
            |
            KW_LABLETHEADER LCURLY LATEXSTRING:s
            {:
                System.out.println("\tsheet:addHeader(\""+s+"\")");
            :}
            RCURLY
            |
            KW_LABLETCHECK LCURLY LATEXSTRING:s
            {:
                System.out.println("\tsheet:addCheckQuestion(\""+s+"\")");
            :}
            RCURLY
            |
            KW_LABLETVIDEO LCURLY LATEXSTRING:s1 RCURLY LCURLY LATEXSTRING:s2
            {:
                ++videonumber;
                System.out.println("\tlocal cameraExperiment"+videonumber+" = sheet:addCameraExperiment()");
                System.out.println("\tcameraExperiment"+videonumber+":setDescriptionText(\""+s2+"\")");
                System.out.println("\tlocal "+s1+" = cameraExperiment"+videonumber+":getExperiment()");
            :}
            RCURLY
            |
            horizontal
            ;
named_elems ::=
            named_elem named_elems
            | named_elem ;
named_elem ::=
            KW_LABLETTEXT LCURLY LATEXSTRING:s
            {:
                System.out.println("\tsheet:addText(\""+s+"\", "+element+")");
            :}
            RCURLY
            |
            KW_LABLETHEADER LCURLY LATEXSTRING:s
            {:
                System.out.println("\tsheet:addHeader(\""+s+"\", "+element+")");
            :}
            RCURLY
            |
            KW_LABLETCHECK LCURLY LATEXSTRING:s
            {:
                System.out.println("\tsheet:addCheckQuestion(\""+s+"\", "+element+")");
            :}
            RCURLY
            |
            horizontal
            ;
horizontal ::=
            KW_HORIZONTALTWO
            {:
                Integer h = ++horizontalnumber;
                Integer n = sheetnumber;
                element = "horizontalLayout";
                System.out.println();
                System.out.println("\tlocal "+element+" = sheet:addHorizontalGroupLayout()");
            :}
            LCURLY vertical RCURLY
            LCURLY vertical RCURLY
            |
            KW_HORIZONTALTHREE
            {:
                Integer h = ++horizontalnumber;
                Integer n = sheetnumber;
                element = "horizontalLayout";
                System.out.println();
                System.out.println("\tlocal "+element+" = sheet:addHorizontalGroupLayout()");
            :}
            LCURLY vertical RCURLY
            LCURLY vertical RCURLY
            LCURLY vertical RCURLY
            ;
vertical ::=
            {:
                Integer v = ++verticalnumber;
                prev = element;
                element = "verticalLayout";
                System.out.println();
                System.out.println("\tlocal "+element+" = sheet:addVerticalGroupLayout("+prev+")");
            :}
            named_elems
            {: element = prev; :}
            ;
