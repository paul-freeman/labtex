/* CUP file for generating a parser */
/* http://www2.cs.tum.edu/projects/cup/ */

package nz.ac.auckland.labtex.generated;
import java_cup.runtime.*;
class LabParser;
action code {:
    Integer sheetnumber = 0;
    String element = null;
:}

/* basics */
terminal        WHITESPACE, BLANKLINE;
terminal String LATEXSTRING, LATEX_CMD, LATEX_HREF, LATEX_FOOTNOTE;

/* commands */
terminal        CMD_TITLE,
                CMD_SHEET,
                CMD_MOTIONSHEET,
                CMD_CALCYSHEET,
                CMD_HEADER,
                CMD_CHECK,
                CMD_TEXT,
                CMD_VIDEO,
                CMD_MOTIONANALYSISGRAPH,
                CMD_EXPORT;

/* symbols */
terminal        AMPERSAND,
                PERCENT,
                DOLLARSIGN,
                HASHMARK,
                UNDERSCORE,
                LEFTCURLYBRACE,
                RIGHTCURLYBRACE,
                TILDE,
                CARROT,
                BACKSLASH,
                ESCAPED_BACKSLASH;

/* non-terminals */
non terminal String         latex_cmd, tex_file, title;
non terminal String         sheets, sheet, normal_sheet, video_sheet, motion_sheet, calc_y_sheet;
non terminal String         find_cmd, find_left;
non terminal String         latex_string, string_piece;
non terminal String         sheet_elems, sheet_elem, sheet_elem_opt;
non terminal String         curly_elems, curly_elem;
non terminal String         curly_braced, dollar_env, dollar_string, dollar_piece;
non terminal String         ddollar_env, ddollar_string, ddollar_piece;
non terminal String         cmd_title, cmd_sheet, cmd_videosheet, cmd_motionsheet, cmd_calcysheet ;
non terminal String         cmd_text, cmd_header, cmd_check, cmd_video ;
non terminal String         basic_string;
non terminal String         opt_whitespace, dollarsign, tilde, percent, escaped_backslash;
non terminal String         href, href_string, footnote;
non terminal String         left_curly_brace, right_curly_brace;

/***********
 * grammar *
 ***********/

/* a basic tex file should be a title with a series of sheets */
tex_file            ::= title:t sheets:xs                           {: RESULT = t + "\n" + xs + "\nend"; :}
                        ;
sheets              ::= sheets:xs sheet:s                           {: RESULT = xs + "\n\n" + s; :}
                        | sheet:s                                   {: RESULT = s; :}
                        ;
/* there are a number of sheet types available */
sheet               ::= normal_sheet:s                              {: RESULT = s; :}
                        | video_sheet:s                             {: RESULT = s; :}
                        | motion_sheet:s                            {: RESULT = s; :}
                        | calc_y_sheet:s                            {: RESULT = s; :}
                        ;




/* curly brace { ... } environment */
curly_braced        ::= left_curly_brace curly_elems:s right_curly_brace:w    {: RESULT = s + w; :}
                        ;
curly_elems         ::= curly_elems:ls curly_elem:s                         {: RESULT = ls + s; :}
                        | curly_elem:s                                      {: RESULT = s; :}
                        ;
curly_elem          ::= basic_string:s                                      {: RESULT = s; :}
                        | curly_braced:s                                    {: RESULT = s; :}
                        | dollar_env:s                                      {: RESULT = s; :}
                        | ddollar_env:s                                     {: RESULT = s; :}
                        | latex_cmd:s                                       {: RESULT = s; :}
                        | UNDERSCORE                                        {: RESULT = "_"; :}
                        | CARROT                                            {: RESULT = "^"; :}
                        ;


/* dollar (math mode) $ ... $ environment */
dollar_env          ::= dollarsign dollar_string:ls DOLLARSIGN WHITESPACE   {: RESULT = ls + " "; :}
                        | dollarsign dollar_string:ls DOLLARSIGN            {: RESULT = ls; :}
                        ;
dollar_string       ::= dollar_string:ls dollar_piece:s                     {: RESULT = ls + " " + s; :}
                        | dollar_piece:s                                    {: RESULT = s; :}
                        ;
dollar_piece        ::= basic_string:ls                                     {: RESULT = ls; :}
                        | latex_cmd                                         {: RESULT = ""; :}
                        | curly_braced:s                                    {: RESULT = s; :}
                        | UNDERSCORE                                        {: RESULT = "_"; :}
                        | CARROT                                            {: RESULT = "^"; :}
                        ;

/* double dollar $$ ... $$ environment */
ddollar_env         ::= DOLLARSIGN dollarsign
                        ddollar_string:ls
                        DOLLARSIGN dollarsign                               {: RESULT = ls; :}
                        ;
ddollar_string      ::= ddollar_string:ls ddollar_piece:s                   {: RESULT = ls + " " + s; :}
                        | ddollar_piece:s                                   {: RESULT = s; :}
                        ;
ddollar_piece       ::= basic_string:ls                                     {: RESULT = ls; :}
                        | latex_cmd                                         {: RESULT = ""; :}
                        | curly_braced:s                                    {: RESULT = s; :}
                        ;


/* once a Lablet title command is found
 * we use the next curly braced string
 * as the title */
title               ::= cmd_title latex_string:ls
                        {:  RESULT =
                            "Lablet = {\n\tinterface = 1.0,\n\ttitle = " +
                            "\"(TeX) " + ls + "\"\n}\n\n\n" +
                            "function Lablet.buildActivity(builder)"; :}
                        RIGHTCURLYBRACE find_cmd
                        ;
find_left           ::=   find_left BACKSLASH
                        | find_left LATEXSTRING
                        | find_left WHITESPACE
                        | /* empty */
                        ;
find_cmd            ::=   find_cmd LEFTCURLYBRACE
                        | find_cmd RIGHTCURLYBRACE
                        | find_cmd LATEXSTRING
                        | find_cmd BACKSLASH
                        | find_cmd WHITESPACE
                        | find_cmd BLANKLINE
                        | /* empty */
                        ;



normal_sheet        ::= cmd_sheet latex_string:ls RIGHTCURLYBRACE find_cmd sheet_elems:xs
                        {:  ++sheetnumber;
                            RESULT =
                            "    -- SHEET " + sheetnumber + "\n" +
                            "    local sheet = builder:create(\"Sheet\")\n" +
                            "    builder:add(sheet)\n" +
                            "    sheet:setTitle(\"" + ls + "\")\n" +
                            xs; :}
                        ;
video_sheet         ::= cmd_videosheet latex_string:ls RIGHTCURLYBRACE find_cmd sheet_elems:xs
                        {:  ++sheetnumber;
                            RESULT =
                            "    -- SHEET " + sheetnumber + "\n" +
                            "    local sheet = builder:create(\"Sheet\")\n" +
                            "    builder:add(sheet)\n" +
                            "    sheet:setTitle(\"" + ls + "\")\n" +
                            xs; :}
                        ;
motion_sheet        ::= cmd_motionsheet latex_string:l1 right_curly_brace
                        left_curly_brace latex_string:l2 right_curly_brace
                        left_curly_brace latex_string:l3 right_curly_brace
                        {:  ++sheetnumber;
                            RESULT =
                            "    -- SHEET " + sheetnumber + "\n" +
                            "    local sheet = builder:create(\"MotionAnalysis\")\n" +
                            "    builder:add(sheet)\n" +
                            "    sheet:setTitle(\"" + l1 + "\")\n" +
                            "    sheet:setExperiment(" + l2 + ")\n" +
                            "    sheet:setDescriptionText(\"" + l3 + "\")\n"; :}
                        ;
calc_y_sheet        ::= cmd_calcysheet latex_string:l1 right_curly_brace
                        left_curly_brace latex_string:l2 right_curly_brace
                        left_curly_brace latex_string:l3 right_curly_brace
                        {:  ++sheetnumber;
                            RESULT =
                            "    -- SHEET " + sheetnumber + "\n" +
                            "    local sheet = builder:create(\"CalculateYSpeed\")\n" +
                            "    builder:add(sheet)\n" +
                            "    sheet:setTitle(\"" + l1 + "\")\n" +
                            "    sheet:setExperiment(" + l2 + ")\n" +
                            "    sheet:setHeader(\"" + l3 + "\")\n"; :}
                        ;




sheet_elems         ::= sheet_elems:xs sheet_elem:s                         {: RESULT = xs + s; :}
                        | sheet_elem:s                                      {: RESULT = s; :}
                        ;
sheet_elem          ::= sheet_elem_opt:s BLANKLINE find_cmd                 {: RESULT = s; :}
                        | sheet_elem_opt:s                                  {: RESULT = s; :}
                        ;
sheet_elem_opt      ::= cmd_text latex_string:ls                            {: RESULT = "    sheet:addText(\"" + ls + "\")\n"; :}
                        | cmd_header latex_string:ls                        {: RESULT = "    sheet:addHeader(\"" + ls + "\")\n"; :}
                        | cmd_check latex_string:ls                         {: RESULT = "    sheet:addCheckQuestion(\"" + ls + "\")\n"; :}
                        | CMD_EXPORT                                        {: RESULT = "    local exportButton = sheet:addExportButton()\n"; :}
                        | cmd_video left_curly_brace basic_string:name right_curly_brace latex_string:ls
                            {: RESULT =
                               "    local video = sheet:addCameraExperiment()\n" +
                               "    video:setDescriptionText(\"" + ls + "\")\n" +
                               "    local " + name + " = video:getExperiment()\n"; :}
                        | CMD_MOTIONANALYSISGRAPH left_curly_brace basic_string:exp right_curly_brace
                                                  left_curly_brace basic_string:type right_curly_brace
                            {: RESULT = "    sheet:addMotionAnalysisGraph(" + exp + "):" + type + "()\n"; :}
                        ;


/* stuff that LaTeX treats as text */
latex_string        ::= latex_string:ls string_piece:s              {: RESULT = ls + s; :}
                        | string_piece:s                            {: RESULT = s; :}
                        ;
string_piece        ::= basic_string:s                              {: RESULT = s; :}
                        | latex_cmd:s                               {: RESULT = s; :}
                        | dollar_env:s                              {: RESULT = s; :}
                        | ddollar_env:s                             {: RESULT = s; :}
                        | curly_braced:s                            {: RESULT = s; :}
                        | tilde                                     {: RESULT = " "; :}
                        | percent                                   {: RESULT = "%"; :}
                        | escaped_backslash                         {: RESULT = "\n"; :}
                        | href:link                                 {: RESULT = ""; :}
                        | footnote:s                                {: RESULT = s; :}
                        ;

href                ::= LATEX_HREF LEFTCURLYBRACE href_string:link RIGHTCURLYBRACE      {: RESULT = link; :};
href_string         ::= href_string:xs basic_string:x                                   {: RESULT = xs + x; :}
                        | href_string:xs UNDERSCORE                                     {: RESULT = xs + "_"; :}
                        | basic_string:x                                                {: RESULT = x; :}
                        ;
footnote            ::= LATEX_FOOTNOTE LEFTCURLYBRACE latex_string right_curly_brace:s  {: RESULT = s; :};

/* titles and sheets that have curly braced arguments */
cmd_title           ::= CMD_TITLE find_left left_curly_brace ;
cmd_sheet           ::= CMD_SHEET find_left left_curly_brace ;
cmd_motionsheet     ::= CMD_MOTIONSHEET find_left left_curly_brace ;
cmd_calcysheet      ::= CMD_CALCYSHEET find_left left_curly_brace ;

/* atomic units that are not divided */
cmd_text            ::= CMD_TEXT opt_whitespace;
cmd_header          ::= CMD_HEADER opt_whitespace;
cmd_check           ::= CMD_CHECK opt_whitespace;
cmd_video           ::= CMD_VIDEO opt_whitespace;

left_curly_brace    ::= LEFTCURLYBRACE opt_whitespace ;
right_curly_brace   ::= RIGHTCURLYBRACE opt_whitespace:s                    {: RESULT = s; :} ;
latex_cmd           ::= LATEX_CMD opt_whitespace:s                          {: RESULT = s; :} ;
dollarsign          ::= DOLLARSIGN opt_whitespace ;
tilde               ::= TILDE opt_whitespace ;
percent             ::= PERCENT opt_whitespace ;
escaped_backslash   ::= ESCAPED_BACKSLASH opt_whitespace                    {: RESULT = "\n"; :} ;
basic_string        ::= LATEXSTRING:ls opt_whitespace:s                     {: RESULT = ls + s; :} ;

opt_whitespace      ::= opt_whitespace WHITESPACE                           {: RESULT = " "; :}
                        |                                                   {: RESULT = ""; :}
                        ;

